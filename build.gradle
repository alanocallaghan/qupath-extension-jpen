plugins {
  id 'java-library'
  // To create a shadow/fat jar, including dependencies
  id 'com.github.johnrengelman.shadow' version '7.0.0'
}

ext.moduleName = 'qupath.extension.jpen'
archivesBaseName = 'qupath-extension-jpen'
description = "QuPath extension to support graphic tablet input using JPen."

version = "0.3.0-SNAPSHOT"

def libName = "jpen-2.0.0"
def libZipped = file("libs/${libName}-lib.zip")
def libUnzipped = file("${buildDir}/unpacked/")

task extractLibs(type: Copy) {
    from zipTree(libZipped)
    into libUnzipped
}

compileJava.dependsOn(extractLibs)

repositories {
  mavenCentral()
  
  flatDir {
    dirs "${buildDir}/unpacked/${libName}/"
  }
  
  maven {
    name = "GitHubPackages"
    url = uri("https://maven.pkg.github.com/qupath/qupath")
    credentials {
      username = project.findProperty("github.packages.user") ?: System.getenv("USERNAME")
      password = project.findProperty("github.packages.key") ?: System.getenv("TOKEN")
    }
  }
  
}

dependencies {  
  implementation name: "jpen", version: "2"
  
  shadow "io.github.qupath:qupath-gui-fx:0.3.0-SNAPSHOT"
  shadow "org.slf4j:slf4j-api:1.7.25"
}

jar {
  from ("${buildDir}/unpacked/${libName}") {
    into 'natives/'

    include '*.dll'
    include '*.jnilib'
    include '*.so'
  }
}

shadowJar {
  from ("${buildDir}/unpacked/${libName}") {
    into 'natives/'
    
    include '*.dll'
    include '*.jnilib'
    include '*.so'
  } 
}
